v load permanent fingerprints and remap            v 0*aa0)S+*aa20**aaa*aa" "<
>"LLUN"4#v($$v                                     > 0"89b.tognuf"#@i$$$$     v
v        ># #<0a".detroppus ton LLUN">:#,_@              (^reload)
>"GNIF"4#v(v
v        >#<0a".detroppus ton GNIF">:#,_@
>"NRTS"4#v($$'S'UZv
v   ZQ'C'>#      #<0a".detroppus ton NRTS">:#,_@
>"KCOS"4#v($$v
v  YL'YA'># #<0a".detroppus ton KCOS">:#,_@
>"EKCS"4#v($$v
v     YP'># #<0a".detroppus ton EKCS">:#,_@

 initialize funge-space state
>'G'KX'P'BX'L'IX"ELIF"4#v(0000"tad.tognuf/atad"#vOv
v -a0 p***aaa200        <  >#  ># >#    >#     #<   >0a".dab "v
>  >:0\0\p v  defs    v   _^#:G^#g 15p1e :<-a0 p15<   v"State"<
v$_^#!: +1 <          > 1-I0e1gB $ 1+ :   |           >:#,_@
v)        Cg15 p\0\0 $$$>#        ^#<000< >$2aaa***v
>'G'KX'P'BX'L'IX)v   v:G^#Bg1e0I-1 _^#:G^#g15 p1e: < +2<
v                <   >!#^_1-IV0e1g1+:e1pp$ 1>: v       $
               >1+:51g#vG:!#v_1-IV\$\e1gp 1+^
               |  gg1e: pg1e \$\VI-1_v#:Gv#g15 <
                       >    >        >   >          ^
               >                                       ^
 save settings from the loader
>  f2P  41p  #vIv
v G1fP1f   p10>#<0a".sserdda PI dilavnI">:#,_@

 Build initial login packet at (0,0)
>0" KCIN"00PN5+11p50P11g:d\0p1+:a\0p1+11p0ad   v
vp11++2*93g11P0g11"USER fungot * * :fungot-0.1"<
 Create a socket and connect to server
>221#vSv
vp12:>#<0a".tekcos a etaerc ot elbanU">:#,_@
>20"7666"V01g#vCv
v             >#<0a".tcennoc ot elbanU">:#,_@
 Send out the registration packet.
>0011g21g#vW11g-!#v_0a".deliaf dnes kciN">:#,_@
v         >#     #<^
 Main dispatch loop: parse IRC messages.
>0:02p12pv
v        <    >     >0a".deliaf daer CRI">:#,_@
>12g0fff**21g#^R:#v_^
 vp11             <                                                         .
 0      >v                                                                  .
 >:11g\`|  coming here if this byte needs to be processed     emptycmd      .
 v      <> :12g+0g :a-#v_     v  and here if newline           v   0<       .
 #                     >:d-#v_> $:0\12g+0p 02g0G03P :12g+1+ 02p 03g!|       .
 ^+1$                       <                                  <            .
  end of bytes from irc                                                     .
 >12g+12pv  updated eptr                                                    .
 v       <                                      toggle to print raw:        .
  have to memmove message                     v$_,#! #:<"RAW >>> "0 <       .
      v      +1 p0 -g20 <                     >03G>:#,_$v                   .
 >02g >:12g\`! #v_ ::0g ^                      v" <<<"a0<                   .
^    p200p21-g20<                              >:#,_$               v       .
                            0 <-- dummy value to discard up there           .
  irc message handler       ^ return here when finished                     .
v>#                         ^#                                      <       .
                                                                            .
  check for prefix  v     +1<                                               .
> 03g':-#v_104p    1>:3g" "-|                                               .
v  0 p400< v     P41G31p3\0:<                                               .
v +1       <                                                                .
                                                                            .
  command start pos at stack                                                .
  find next space or 0                                                      .
> :31p>:3g:#v_     >\ :0\3p 31g3G15P v                                      .
            >:" "-!|                                                        .
      ^         +1$<                                                        .
v _v#!                         p13+1 <                                      .
0  1
0  0 command ends in nonzero, so at least one option
5  5 parse the options in a loop, :opt ends
p  p   v       <
   >31g :3g':-#^_ 1+          0\:v
       >:3g:#v_     >\ :0\3p 31g >3G 05g5+0\P v
             >:" "-!|
       ^         +1$<
   ^                   p50+1g50 _v#     p13+1 <
v                                < end of options

  irc message has been split, process it

 ^                                     _v#!-e<
  ping? pong!                           >  v
> 15G0"GNIP"Q#v_0ad"tognuf: GNOP"03P03e21g#vW^
v             <      v"Pong send failed."a0<
                     >:#,_@
 ^              <               <                                   <
  privmsg!
> 15G0"GSMVIRP"Q| parse privmsg   target/prefix for reply
                >06g'#-!#v_ 04g!|  v+1           <    > >           ^
                v        <p4\!'<> 1>:4g:!#v_ '!- |           privmsg print
                               |  P60 G41 <1p4\0:<     v           _^#-g17g16  <
                v              _ d-if                 $> 61g:d\3p1+: a\3p1+ v
                  build reply prefix                  ^W^#          g12\3\0 <
   v_v#!-g14 <  >0": "06GA0" GSMVIRP"A v
      cmdchk ^g70         P30 p17p16:N <

      non-command privmsg handling
      catch-all own-name hook
 ^             <
     >f1G07GF!#^_>_ 0"!iH"61g:3+61p3P                                          ^

     actual command-character commands
 ^                       <         <  < <        <
    "^reload"      >04g!#^_f2G14GQ#^_    "SYOT"4#^(                          ^
   >17G 0"daoler"Q!|   prefix check   ^W^#     g12\3\0+bg16P3g16"Reloaded."ad0<
   v               <                                                          >^
 ^                      <         < <                                           
    "^raw"        >04g!#^_f2G14GQ#^_ 57GN51p03P 51g:d\3p1+:a\3p1+ v             
   >17G4L0" war"Q!|                   >    >0a".deliaf dnes waR"   >:#,_@       
   v              <                 ^_^#!-W^#            g12\3\0: <             
 ^                       <         <         <                     <            
    "^code"        >04g!#^_f2G14GQ#^_"RBUS"4#^( 0"R0"67GA08P 080C )^            
   >17G5L0" edoc"Q!|                                                            
   v               <                                                          >^
 ^                    <  <          <                  )<
    "^save"     >04g!#^_v  >"GNIF"4#^('G'KX'P'BX"ELIF"4#^(             001 v
   >17G0"evas"Q!|v G2f  <   vPv#AK\0\a\0\g15:<-a0 p15Ov#"data/fungot.dat"0 <
   v            <> 14G Q#^_^>$           1+:#^_$vv_v#gg1e:$Pv#AUgg1e\a\0\g15 <
     v       B80"Fail: "<S'0$C< v!g\0:< ***aaa2 <>1 +:51g\0\ a\e1ggUA#vP$v   :
     v         B80"OK."0  Cg15 $_v    ^+1         $<    v$\ <         <  >1+ ^
                                 >:51g\0\a\0\KA#vP$1+:e1 p:0\g51g\0\a\UA#vP$1^
                        ^                  P'0$C<       <               <
     >                                       )'G'KX'P'BX)08GN51p61g:51g+61p3P ^


    "^help"      >0"]txet[ dda/tes/teg 9-0 rts"41g" ;N:rts/txet=edoc ,fb=g"v
   >17G0"pleh"Q>!| v "ef <command> <lang> <code>; "g14"show [command]; lan"<
   v             < > "d"41g" ;>edoc< >gnal<"41g           N51p61g3P61g51g+61p ^

    "^str"        >67g" "-!#v_> >   >0"]txet[ dda/tes/teg 9-0 rts"41gv
   >17G4L0" rts"Q!|  v-0'g75< $ $   $                      v"Usage: "<
   v              <  >     :9`|                            >N51p61g:51g+61p3P ^
                              >:0\`#^_a- 7>:7g:" "-#v_$ v
 ^                >$            ^v p800 $_^#!   \+1\<
    v_v#Q"set"0G77 _v#Q"get"0G77 <        P80G7+1 p7\0: <
    >7 7G0"dda"Q  | >                     >:0\g!#v_  0\G   >N51p 61g:51g+61p3P^
      >51p08G051gP 0" :teS"61g:5+61p3P 51g^      >0".ytpmE"^
                  >:51p 0\GNe1p 051gP 08Ge1g51gP 0".deddA"61g:6+61p3P         ^

 ^                                           <                                 |
    "^bf"        >           61g71p 0480 vv                                    <
   >17G3L0" fb"Q!| vp7\0:_v#!-!'_v#:g7:<4<          vp3\a:+1p3\d:g16<         |#
   v             < >1+ v  >          1+^            >1+0\3\21g#vW$v |-g17g16  <
    v              p15 <        $<           ^                 <  < <         #
     v g8080                              <
 ^                          <<
      "^show"       > 2aaa*** 57g:#v_$>:0\g#v_$                              0^
   >  17G4L0"wohs"Q!|       #| -" "<  ^ +2 <>:0\" "\0\GAN51p v
   v                <    >2+ >:0\G67GQv    ^ p16+g15g16P3g16 <
                         ^ _^#g\0:    _ 1+:0\g #v_e1p 1> :e1gg0\ jvvvvvvv
 ^                                              <      +          >        $$0^
                                                       1           ''''''
                         > 61g:51g+61p3P >             ^           >+[].,
                         ^   p15NUgg1e: _^#g15 +1 P3p16+1:g16 p15<0  <<<<
                                                                 ^1<<
                               v                              $<
      "^def"        >15>:7g:#v_>$$$0">edoc< >gnal< >dnammoc< fed" 41g" :egas"v
   >  17G4L0" fed"Q!|        >  " "-#v_:0\7p1+57G08P:>:7g:#v_$ ^ vP3g16p15NU'<
   v                <  ^+1           <            >1+^           >61g51g+61p f^
               v\0:_v#g\0:< ***2aaa P90G7\+1p7\0:_^#  !-" "<                   |
               G    >       :51p08G051gP v > :0\0\p:0\1\p      ^ v"Defined."0$ <
               >08GQ!#v_2+^ v p\0\0+2:   <                       >61g3Pv       #
                      >     > 1+  09G0"fb"Q|                     v+8g16<       -
       v                          0 p\0\0: <                     >61p        1^1
         code URL handlers hook in here                                        :
       >e1p\v
       v    <

         "str:X"       >:5+7g!#v_      >    0".LRU :rts daB"61g:d+61p3P $$$$ f^
       > :7G4L0":rts"Q!| vg7+4:< >   >$^
       v               < >'0-:0\`|
                                 >:9`|
        v          P7g15G\0-a \p15:\ <

         "http://srv/path">7+:>:7g:#v_$$         0".LRU daB"61g:8+61p3P $$$$ f^
       > :7G7L0"//:ptth"Q!| >+^> :7G 0fP 0\7p v URL parsed
       v                  < ^1_^#-/'< >0".dnu" "of ton tsoH"61g:f+61p3P  $$$ f^
        v                            H^# G7+7 < 

       >>\v
    v  g1e< finished with URL handlers, restore args
 ^
      defines                                  $
   >  1>:7g:" "-#v_$:0\7p1+>51p 2aaa*** >:0\g!#^_ :0\ v
       ^+1       _         ^            ^+2 _v#QG71G  <
     v                            g\0: \0 +1 <

      PROG EXECS:

      exec prog-copy if not at line 8 (maybe check lang)
     >\:8-!#v_:e1p 0\g08p 1> ::e1gg: !#v_\8p v
     v    <$<              ^+1p8\gg1e::   +1 <
     :      ^                       p8\<
     !
     #
     >$ 2aaaaa*****81p 88+:*91p aaa**:a1p >1-:0\v
     |bf exec v10                       $_^#!:p9<
      v\+1\g8:<+1  p18-1_v#:g18 <
      j                  >$$$     0"!emit fo tuo... " 61g3P 61g88++61p        ^
      >$$                                                                     ^
      >:8gb1p\b1g+a1g%\         ^  <>      ret^
      >:8gb1p\::9gb1g+91g%\9p\  ^  +-
      >\:9g v >\                ^  [
     $>\:9g!>#^_\8g             ^  ]    > $$$$ 0"... " 61g1-3P 61g3+61p       ^
     $>\:9g:84*\`  #v_  >61g:1+6 1p:ff*`|   out
     $>\:51g:7g:#v_v>$'.^>\9p\  ^  \ p3 <   in
     $           > \1+51p^
 ^   <             >$    ^

      PROG PREPS:
    :
    !>$ 88+:*91p v
    # v p1a**aaa <             v$$       <
    >^>e1p:0\> :7g :#v_$$!#v_  >$0".][ dehctamsiM" 61g3P 61ge+61p            $f^
    |bf prep         >:'[- #v_$\1+\v     #
                            >']-#v_ \:#v_^
             ^                 +1< <\-1<
              v p181 pg1e00<            v>#                            v#<
      >      #v #+ #1                  #<^#<p18+2g18 pg1e+1g18 pg1eg18 <
              > :7g :#v_     v
        parsed        >:'!-!#v_ >:'<-#v_$1\>:1+7g'<-!#v_\a1g%a1g\-v
        v         pg1eg180 $$<             ^    +1\+1\<
                                      >:'>-#v_$1\>:1+6g'>-!#v_\v  <
          v-\g19%g19\_v#--'g7+1:<\1$_v#!--':<    ^    +1\+1\<  >a1g% 1 ^
        # > v         >\1+\1+   ^        >     >     >   >             ^
      ^2%g19<\_v#-+'g7+1:<\1$_v#!-+':<
        1      >\1+\1+   ^    >:'[-#v_$03^>:'.-#v_$05^>06^
        >:e1gg:#v_$$              v >:']-#^_$04^>',-!#^_                 ^
        ^+2_v#-3<                 >                                            ^
            >  ::1\>2+:e1gg:3-!#v_4-#v_\1-:#v_v
    $   +          ^       \+1\$<v   <     \< $
    $   2          ^             < v   p1b:+1 <
    $   ^       pg1eg1b+1: pg1e+1\ <
 ^  <

fungespace storage:
 (*,0): data packet storage
 (*,1): misc. variables
  (0,1): server IP address
  (1,1): often data packet length
  (2,1): server socket
  (3,1): at message-splitting, start of current command/option
  (4,1): command character
  (5,1): temp
  (6,1): privmsg reply packet prefix len
  (7-10,1): temp
  (15-,1): bot's nickname
 (*,2): socket message-splitting vars
  (0,2): start of message
  (1,2): end of message
  (15-,2): owner prefix
 (*,3): current raw irc message to handle/also reply packet
 (*,4-x): parsed irc message
  (0,4): 1 if prefix, 0 if not
  (1-x,4): the prefix string if (0,4) is 1
  (0,5): amount of command options, 0..15
  (1-x,5): command string (always)
  (*,6-x): command options, depending on amount

code interpretation:
 column 5 is used to go to program prep: stack: retaddr src_x dst_y lang
 returns via column 80 with stack popped      (src_y=7, dst_x=0)
 column 6 is used to go to program exec: stack: retaddr org_y lang
 returns via column 79  (copies prog to y=8 if org_y != 8)
 (5,1): input string pointer (x, y=7)
 (6,1): reply packet current length
 (7,1): reply packet initial length for checking
 (e,1): temp storage for y for progcopy/progprep
 brainfuck bytecode:
  two-cell opcodes: first cell op, second arg. ops:
   0: end program (no arg)
   1: add arg (modulo 1000) to bfarray pointer - used for <>
   2: add arg (modulo 256) to cell value - used for +-
   3: jump to arg if cell zero - used for [
   4: jump to arg if cell nonzero - used for ]
   5: output cell (arg dummy) - used for .
   6: input cell (arg dummy) - used for ,
 brainfuck exec:
  (8,1): cycle limit
  (9,1): 256 - used in % for cell values
  (a,1): 1000 - used in % for bfarray locs
  top of stack (usually): input prog pointer (x, y=8)
  second on stack: data array pointer (x, y=9)
 brainfuck prep:
  in pseudo:
   - check that [] match
   - convert instructions to bytecode with [] args missing
   - scan bytecode and write targets to all [] pairs
  (8,1): output program position

brainfuck interp uses:
 (5,1): input string position (x, y=7)
 (6,1): output message position (x, y=3)
 (7,1): output message start (to check if any; +2 for crlf)
 (8,1): cycle count for limit
 (9,1): 256; for modulo
 (a,1): 255: for modulo
 top of stack (at start): input program position (x, y=7)
 second on stack: input data array position (x, y=8)

bfcmd data block:
 (0,2000) base
 two rows per registered command
 first row has zero-terminated command name
 second row has corresponding brainfuck program

--- this code block prints out parts of the IRC message         ---
--- if you put it at the point where the message has been split ---

> 04g!#v_ 0" :xiferP">:#,_$ 14G>:#,_$ a, v
v      <                                 <
> 0" :dnammoC">:#,_$ 15G>:#,_$ a, v
   v1 $_,#! #:<"arguments:"0.g50  <
   >   :05g` #v_ "' ",,  v
   ^+1,''$_,#! #:<G\0+5: <
 ^         ,a <

--- old brainfuck interpreter kept here for reference for a while ---

                             >$1-     v
    "^bf"        > 4 >:7g:#v_^  >:0\7p>1+51p 081p 88+:*: 91p 1-a1p v
  >>17G3L0" fb"Q!|         >'!-!|       v8\0:-1< **aaa  p17+2g16   <
   v             <   ^        +1<       >p:!#v_^
     v 40                                   $<0 p740  $<           <
     > :0\ > :7g :#v_$$!#v_ >$0ad".][ dehctamsiM" 61g3P   61ge+61p ^
     v                   <  ^       $$<
                   >:'[-#v_$\1+\v
                         >']-#v_ \:#v_^
           ^                +1< <\-1<
               >0ad"!emit fo tuo... "61g3P 61gf1++61p $^
     v +1     _^#!` *****aaaaaa p18:+1g18 <     < <           <  <       <
     > :7g :!#v_ :'+-#v_$ \::8g1+91g%\8p\ ^                  p 3\ a+1p3\d <
     v+2g16$$$<       >:'--#v_$ \::8ga1g+91g%\8p \^                 +crlf :
     >::71g-!#v_0\3\21g v   >:'>-#v_$ \1+aaa**%\^                         g
 ^          $$<_v#!-Wv# <         >:'<-#v_$ \aaa**1-+ aaa**%\ ^           1
       no out^  >    > v v_v#-,':_v#-.':<     > v    +<v\ P3g16"  ..."0<  6
      v"BF W failed."a0< : $      >$ \:8g:ff+`| >61g3p \61g1+:61p ff*`#^_ ^
      >:#,_@             ' >\:51g7g:#v_\8p\  v>$fff1++^> 61g4+61p :1+0\7p ^
      v \1 \_v#g8:\<     [ v+1g15\p8\<       >                   ^
             >\   v      - >51p \>\          ^   >']-#v_\1-:#v_$ ^
      >1-:7g:']-v  |!-]' _$ \:8g#^_\ 1\>1+:7g:'[-|           \
      ^   \+1\$<!  v                   ^    \+1\$<
      ^      < ^_v                     ^              <      <
             |-['<>>  >                                          ^
             >\1-:#v_$^             >0":deretsigeR"v               v15g1630P3g1<
      ^           \<                 vp16+bg16P3g16<               >g+21g #vWv 6
                                            > :0\G 61g :" "\3p 1+:v            p
    "^bfcmd"       > 2aaa*** 67g#v_ ^> :0\g#^_ $61g:d\3p1+ :a\3p1+ 0\3\21g#vWv 1
   >17G5L0"dmcfb"Q!|                 ^+2 _ #!<p16+g16NG\0: P3 p16 <          $ 5
   v               <v    _#                                                < < N
 ^                  <d-if|-" "g76<>$$0                >\  >:0\g#v_\#v_$0ad".oN"^
                         >7>:7g:!#^_" "-#v_:0\7p1+51p1^>2+^
                           ^+1           <       oldcmd|QG77G\0:<
                    ^      Pg170G7g15 p17+1<           ># \# _1+0\a\ d\0\GA    ^
                                           ^ p+2\0\0: g17Pg170G77p17<newcmd
 ^                            $<                                     <
    "^bfadd"        > 7 >:7g:!#^_ " "v                       disc l8
   >17G6L0" ddafb"Q!|   ^+1    _ #- #< :0\7p 1+7G08P 2aaa*** > :0\g!#^_    v
   v                <                                        ^+2_v#QG77G\0:<
 ^                                       Pg150 A Gg150 G80 p15 +1<
 ^                  <
    bfcmd ex v +2   $#<                               $Q<
   > 2aaa*** > :0\g!| ^                      <                   <
                    > :0\G N51p 17GN51g-!#v_" "51g1+7g-#^_ 51gLQ#^_ v
  ^                             <         >Q#^_    0\v
  ^P80 P7g15G80p15+1 p7\!':NG70_^# P74G\0 P71"bf "0+1<\1 P80G7+2g15 <
